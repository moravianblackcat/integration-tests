plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
}

group = 'cz.dan'
version = '4.1.0'

repositories {
    mavenCentral()
}

ext {
    set('assertjVersion', '3.22.0')
    set('cucumberVersion', '7.20.1')
    set('lombokVersion', '1.18.38')
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'io.cucumber:cucumber-java'
    implementation "org.assertj:assertj-core:${assertjVersion}"
    implementation 'org.awaitility:awaitility:4.2.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
}

dependencyManagement {
    imports {
        mavenBom "io.cucumber:cucumber-bom:${cucumberVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/moravianblackcat/integration-tests")
            credentials {
                username = System.getenv("ACTOR")
                password = System.getenv("TOKEN")
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    suppressedValidationErrors.add('dependencies-without-versions')
}